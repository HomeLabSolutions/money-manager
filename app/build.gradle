apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.firebase.crashlytics'

apply from: '../shared_dependencies.gradle'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0
    def versionBuild = 10

    defaultConfig {
        applicationId names.applicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1000 * (1000 * versionMajor + 100 * versionMinor + versionPatch) + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.incremental": "true"]
            }
        }
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
        }
    }

    viewBinding {
        enabled = true
    }
    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    configurations {
        ktlint
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

dependencies {

    implementation project(':core')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.0"

    implementation "com.google.firebase:firebase-auth:19.3.2"
    implementation "com.firebaseui:firebase-ui-auth:6.2.0"
    implementation "com.google.android.gms:play-services-auth:18.1.0"

    implementation "com.google.firebase:firebase-analytics-ktx:17.5.0"
    implementation "com.google.firebase:firebase-crashlytics:17.2.1"

    implementation "com.facebook.android:facebook-login:5.15.3"
    implementation("com.twitter.sdk.android:twitter:3.3.0") {
        transitive = true
    }

    implementation "com.squareup.okhttp3:okhttp:4.8.1"
    implementation "com.squareup.okhttp3:logging-interceptor:4.8.1"

    implementation "com.google.code.gson:gson:2.8.6"

    implementation "com.github.bumptech.glide:glide:4.11.0"

    implementation "com.facebook.stetho:stetho:1.5.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.1"

    implementation "androidx.paging:paging-runtime-ktx:2.1.2"
    implementation "androidx.paging:paging-rxjava2-ktx:2.1.2"
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-rxjava2:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    ktlint "com.pinterest:ktlint:0.37.2"
}
